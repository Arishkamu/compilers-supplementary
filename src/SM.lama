-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ         -> sprintf ("READ")
  | WRITE        -> sprintf ("WRITE")
  | BINOP (s)    -> sprintf ("BINOP %s", s)
  | LD    (x)    -> sprintf ("LD %s", x)
  | ST    (x)    -> sprintf ("ST %s", x)
  | CONST (n)    -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, an SM-configuration and a program,
-- returns a final configuration
fun eval (env, c, insns) {
  case insns of
      {} -> c
    | i:ns ->
      case c of
          [stack, s, w]  ->  case i of
              READ       ->  case readWorld(w) of [z, w1] -> eval (env, [z:stack, s, w1], ns) esac
            | WRITE      ->  case stack of z:stack1 -> eval (env, [stack1, s, writeWorld(z, w)], ns) esac
            | BINOP (op) -> case stack of y:x:stack1 -> eval (env, [(evalOp(op, x, y)):stack1, s, w], ns) esac
            | LD    (x)  -> eval (env, [s(x):stack, s, w], ns)
            | ST    (strr) -> case stack of z:stack1 -> eval (env, [stack1, s <- [strr, z], w], ns) esac
            | CONST (n)    -> eval (env, [n:stack, s, w], ns)
            | LABEL (l)    -> eval (env, c, ns)
            | JMP   (l)    -> eval(env, c, fromLabel(env, l))
            | CJMP  ("z", l) ->
              case stack of
                0:stack1 -> eval(env, [stack1, s, w], fromLabel(env, l))
              | _:stack1 -> eval(env, [stack1, s, w], ns)
              esac
            | CJMP  ("nz", l) ->
              case stack of
                0:stack1 -> eval(env, [stack1, s, w], ns)
              | _:stack1 -> eval(env, [stack1, s, w], fromLabel(env, l))
              esac
          esac
        | _ -> failure ("no SM-comfiguration")
      esac
  esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine instructions
fun compileExpr (expr) {
  case expr of
      Var (x) -> emptyBuffer () <+ LD (x)
    | Const (n) -> emptyBuffer () <+ CONST (n)
    | Binop (op, l, r) -> compileExpr(l) <+> compileExpr(r) <+ BINOP(op)
  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  -- Generates a buffer with an optional label
  --    lab     --- the name of the label
  --    labUsed --- a flag if the lebel is needed
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  -- Generates SM code for a given statement
  --    lab  --- a label th go to after the statement is executed
  --    env  --- compilation environment
  --    stmt --- a statement to generate SM code for
  -- Returns a triple:
  --    boolean flag indicating if "lab" was used
  --    updated environment
  --    code buffer  
  fun compile (lab, env, stmt) {
    case stmt of
      Skip           -> [false, env, emptyBuffer ()]
    | Read  (x)      -> [false, env, singletonBuffer (READ) <+ ST (x)]
    | Write (e)      -> [false, env, compileExpr (e) <+ WRITE]
    | Assn  (x, e)   -> [false, env, compileExpr (e) <+ ST (x)]
    | Seq   (s1, s2) ->
        case env.genLabel of
          [s2Lab, env] ->
             case compile (s2Lab, env, s1) of
               [s2LabUsed, env, s1Code] ->
                  case compile (lab, env, s2) of
                    [labUsed, env, s2Code] -> [labUsed, env, s1Code <+> label (s2Lab, s2LabUsed) <+> s2Code]
                  esac
             esac
        esac
    | If   (e, s1, s2) ->
      case env.genLabel of
        [elsLab, env] ->
          case env.genLabel of
            [eLab, env] ->
            case compile (elsLab, env, s1) of
              [s1LabUsed, env, s1Code] ->
                case compile(eLab, env, s2) of
                  [s2LabUsed, env, s2Code] -> [s2LabUsed, env, compileExpr(e) <+ CJMP("z", elsLab)
                    <+> s1Code <+ JMP(eLab) <+ LABEL(elsLab) <+> s2Code <+ LABEL(eLab)]
                esac
            esac
          esac
      esac
    | While   (e, s) ->
      case env.genLabel of
        [sLab, env] ->
          case env.genLabel of
            [breakLab, env] ->
              case compile (lab, env, s) of
                [labUsed, env, sCode] -> [labUsed, env, emptyBuffer() <+ LABEL(sLab) <+> compileExpr(e)
                  <+ CJMP("z", breakLab) <+> sCode <+ JMP(sLab) <+ LABEL(breakLab)]
              esac
          esac
      esac
    | DoWhile (s, e) ->
      case env.genLabel of
        [sLab, env] ->
          case compile(sLab, env, s) of
            [labUsed, env, sCode] -> [labUsed, env, emptyBuffer() <+ LABEL(sLab) <+> sCode
              <+> compileExpr(e) <+ CJMP("nz", sLab)]
          esac
      esac
    | _              -> failure ("compileSM not implemented\n")
    esac
  }

  case initCompEnv ().genLabel of
    [endLab, env] ->
       case compile (endLab, env, stmt) of
         [endLabUsed, _, code] -> getBuffer $  code <+> label (endLab, endLabUsed)
       esac
  esac
}
